//#RequireContext CSmMode
#Extends "ChaseAlliance/Libs/ModeBase/GameLoop.Script.txt"
#Const CompatibleMapTypes "TrackMania\\TM_Race"

#Include "ChaseAlliance/Libs/Common/Log.Script.txt"
#Include "ChaseAlliance/Libs/Common/Env.Script.txt" as Env
#Include "ChaseAlliance/Libs/Common/Commands.Script.txt" as Commands
#Include "ChaseAlliance/Libs/Common/Users.Script.txt" as UsersL
#Include "ChaseAlliance/Libs/Common/Events.Script.txt" as Events
#Include "ChaseAlliance/Libs/Common/Messages.Script.txt" as Messages
#Include "ChaseAlliance/Libs/Common/Spawn.Script.txt" as Spawn
#Include "ChaseAlliance/Libs/Common/FakeUserController.Script.txt" as Controller
#Include "ChaseAlliance/Libs/ChaseAlliance/GameState.Script.txt" as GameState
#Include "ChaseAlliance/Libs/ChaseAlliance/Players.Script.txt" as PlayersL
#Include "ChaseAlliance/Libs/ChaseAlliance/Settings.Script.txt" as Settings
#Include "ChaseAlliance/Libs/ChaseAlliance/ChaseTeams.Script.txt" as ChaseTeams
#Include "ChaseAlliance/Libs/ChaseAlliance/UI/PauseUI.Script.txt" as PauseUI
#Include "ChaseAlliance/Libs/ChaseAlliance/UI/MainUI.Script.txt" as MainUI
#Include "ChaseAlliance/Libs/ChaseAlliance/UI/ScoresTableUI.Script.txt" as ScoresTableUI
#Include "ChaseAlliance/Libs/ChaseAlliance/UI/MarkersUI.Script.txt" as MarkersUI
#Include "TextLib" as TL

#Setting S_MapIntroPause 3000 as "Pause in ms for the map intro"
#Setting S_EndRoundPause 4000 as "Pause in ms after a round"
#Setting S_EndMapPause 5000 as "Pause in ms after a map"
#Setting S_EndMatchPause 6000 as "Pause in ms after a match"

#Setting S_RelayerCount 3 as "Number of players that do relays per team"
#Setting S_RelayGap 3 as "Number of failed relay a team can fall behind before getting eliminated"
#Setting S_RelaySortDelay 250 as "Cache duration in ms in order for checkpoint events to be sorted in order"
#Setting S_MapCount 3 as "Number of maps to win a match"
#Setting S_RoundCount 3 as "Number of round to win a map"
#Setting S_LapCount 5 as "Number of laps to win a round"

***StartServer***
***
Log("StartServer");
Env::ToDev();
Commands::Load();
UsersL::Load();
Events::Load();
Messages::Load();
Spawn::Load();
Controller::Load();
GameState::Load();
PlayersL::Load();
Settings::Load();
ChaseTeams::Load();
PauseUI::Load();
MainUI::Load();
// ScoresTableUI::Load();
MarkersUI::Load();
Commands::AddPermittedUser("pTuyJG9STcCN_11BiU3t0Q");
Commands::AddPermittedUser("V_vWwMxbQqO0GgDYtK3zTg");
***

***StartScript***
***
Log("StartScript");
***

***StartMatch***
***
Log("StartMatch");
ChaseTeams::ResetTeamsMapPoint();
declare Integer WinningClan = 0;
***

***BeforeLoadMap***
***
Log("BeforeLoadMap");
***

***AfterLoadMap***
***
Log("AfterLoadMap");
Spawn::Find();
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Intro;
NextYieldSleepMs = S_MapIntroPause;
---Yield---
***

***StartMap***
***
Log("StartMap");
ChaseTeams::ResetTeamsRoundPoint();
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Manual;
***

***StartRound***
***
Log("StartRound");
//ValidateSettings
Settings::SetRelayerCount(S_RelayerCount);
Settings::SetRelayGap(S_RelayGap);
Settings::SetRelaySortDelay(S_RelaySortDelay);
Settings::SetMapCount(S_MapCount);
Settings::SetRoundCount(S_RoundCount);
Settings::SetLapCount(S_LapCount);
Settings::ToNet();
ChaseTeams::ResetTeamsRelay();
ChaseTeams::ToNet();
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;

StartTime = Now + 1500;
if(GameState::IsWaiting() && ChaseTeams::AreComplete()){
	GameState::SetPlaying();
}
if(GameState::IsPlaying() && !ChaseTeams::AreComplete()){
	GameState::SetWaiting();
	Messages::SendStatus(UIManager.UIAll,_("Waiting for players"),0, CUIConfig::EUISound::Warning);
}
PlayersL::SpawnAll(StartTime);

***

***PlayLoop***
***
if(GameState::IsPlaying()){
	// ChaseTeams::PlayLoop();
	if(ChaseTeams::GetWinState() >= 0){
		GameLoop::EndRound();
	}
}
if(GameState::IsWaiting()){
	if(ChaseTeams::AreComplete()){
		// Log("Waiting -> Teams are complete");
		GameLoop::EndRound();
		Messages::SendStatus(UIManager.UIAll,_("Starting in..."),S_EndRoundPause,CUIConfig::EUISound::Warning);
	}
}
***

***EndRound***
***
Log("EndRound");
UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;
Messages::ClearAll();
WinningClan = ChaseTeams::GetWinClan();
if(WinningClan > 0){
	ChaseTeams::IncrementTeamRoundPoint(WinningClan);
	Messages::SendStatus(UIManager.UIAll,ChaseTeams::GetWinStateString(),S_EndRoundPause,CUIConfig::EUISound::Warning);
	Messages::SendBig(UIManager.UIAll,"$"^TL::ColorToText(Teams[WinningClan-1].ColorUI)^Teams[WinningClan-1].Name^" wins the round." ,S_EndRoundPause,CUIConfig::EUISound::VictoryPoint );
	if(ChaseTeams::GetTeamRoundPoint(1) >= Settings::GetRoundCount()){
		ChaseTeams::IncrementTeamMapPoint(1);
		GameLoop::EndMap();
	}
	ChaseTeams::ToNet();
}

NextYieldSleepMs = S_EndRoundPause;
---Yield---
***

***EndMap***
***
Log("EndMap");
UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;
if(WinningClan > 0){
	Messages::SendBig(UIManager.UIAll,"$"^TL::ColorToText(Teams[WinningClan-1].ColorUI)^Teams[WinningClan-1].Name^" wins the map." ,S_EndMapPause,CUIConfig::EUISound::EndRound);
	if(ChaseTeams::GetTeamMapPoint(1) >= Settings::GetMapCount()){
		GameLoop::EndMatch();
	}
}
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::ForcedVisible;
NextYieldSleepMs = S_EndMapPause;
---Yield---
***
	
***EndMatch***
***
Log("EndMatch");
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Podium;
if(WinningClan > 0){
	Messages::SendBig(UIManager.UIAll,"$"^TL::ColorToText(Teams[WinningClan-1].ColorUI)^Teams[WinningClan-1].Name^" wins the match." ,S_EndMatchPause,CUIConfig::EUISound::EndMatch);
}
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Manual;
NextYieldSleepMs = S_EndMatchPause;
---Yield---
***

***BeforeUnloadMap***
***
Log("BeforeUnloadMap");
***

***AfterUnloadMap***
***
Log("AfterUnloadMap");
***

***EndScript***
***
Log("EndScript");
***

***BeforeYield***
***
foreach (Event in UIManager.PendingEvents) {
	Commands::PassUIEvent(Event);
	Controller::PassUIEvent(Event);
	PlayersL::PassUIEvent(Event);
}
foreach (Event in PendingEvents){
	Commands::PassModeEvent(Event);
	ChaseTeams::PassModeEvent(Event);
	PlayersL::PassModeEvent(Event);
}
foreach (EventTime => Events in Events::GetPendingSorted()) {
	ChaseTeams::PassLibSortedEvent(EventTime,Events);
}
foreach (Event in Events::GetPending()) {
	GameLoop::PassLibEvent(Event);
	UsersL::PassLibEvent(Event);
	Controller::PassLibEvent(Event);
	GameState::PassLibEvent(Event);
}
Controller::Yield();
Messages::Yield();
ChaseTeams::Yield();
***

Void NOOP(){}