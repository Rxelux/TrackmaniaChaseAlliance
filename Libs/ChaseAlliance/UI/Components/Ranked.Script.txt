#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink
#Include "ChaseAlliance/Libs/Common/UI/Consts/Align.Script.txt" as Align
#Include "ChaseAlliance/Libs/ChaseAlliance/Consts/Colors.Script.txt" as Colors
#Include "ChaseAlliance/Libs/ChaseAlliance/UI/Components/Button.Script.txt" as Button
#Include "TextLib" as TL

#Const C "Ranked"

Text _Model (){
	return """
<framemodel id="{{{C}}}" class="{{{C}}}" {{{Align::CC}}}>
	<frameinstance modelid="{{{Button::C}}}" id="join-ranked" data-pos="0 13" data-text="{{{_("Join Ranked")}}}" data-text-size="4"/>
	<frameinstance modelid="{{{Button::C}}}" id="leave-ranked" data-pos="0 0" data-text="{{{_("Leave Ranked")}}}" data-text-size="4"/>
	<frameinstance modelid="{{{Button::C}}}" id="start-ranked" data-pos="0 -13" data-text="{{{_("Ready")}}}" data-text-size="4"/>
</framemodel>
""";
}

Text _Directives(){
	return """//!
#Struct K_{{{C}}}{
	CMlFrame Root;
	K_{{{Button::C}}} JoinRanked;
	K_{{{Button::C}}} LeaveRanked;
	K_{{{Button::C}}} StartRanked;
}
""";
}

Text _Functions(){
	return """//!

K_{{{C}}} {{{C}}}_Get(CMlFrame _Parent,Text _Id) {
	declare CMlFrame Root <=> (_Parent.GetFirstChild(_Id) as CMlFrame);
	declare K_{{{Button::C}}} JoinRanked = {{{Button::C}}}_Get(Root,"join-ranked");
	declare K_{{{Button::C}}} LeaveRanked = {{{Button::C}}}_Get(Root,"leave-ranked");
	declare K_{{{Button::C}}} StartRanked = {{{Button::C}}}_Get(Root,"start-ranked");
	return K_{{{C}}}{
		Root = Root,
		JoinRanked = JoinRanked,
		LeaveRanked = LeaveRanked,
		StartRanked = StartRanked
	};
}

Void {{{C}}}_Enter(K_{{{C}}} _Ranked,Integer _StartTime){
	{{{Button::C}}}_Enter(_Ranked.JoinRanked,_StartTime);
	{{{Button::C}}}_Enter(_Ranked.LeaveRanked,_StartTime+20);
	{{{Button::C}}}_Enter(_Ranked.StartRanked,_StartTime+40);
}

Void {{{C}}}_Exit(K_{{{C}}} _Ranked,Integer _StartTime){
	{{{Button::C}}}_Exit(_Ranked.JoinRanked,_StartTime);
	{{{Button::C}}}_Exit(_Ranked.LeaveRanked,_StartTime+20);
	{{{Button::C}}}_Exit(_Ranked.StartRanked,_StartTime+40);
}
""";
}

Text _Labels(){
	return """//!
***{{{Button::C}}}_Event***
***
if(Event.Type == CMlScriptEvent::Type::MouseClick){
	//check if the button event is from one of this component
	if(Button.Root.Parent.HasClass("{{{C}}}")){
		switch(Button.Root.ControlId){
			case "join-ranked": {
				+++{{{C}}}_JoinRankedClick+++
			}
			case "leave-ranked": {
				+++{{{C}}}_LeaveRankedClick+++
			}
			case "start-ranked": {
				+++{{{C}}}_StartRankedClick+++
			}
		}
	}
}
***
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Button::Load();
	Manialink::CreateComponent(C,[Button::C],_Model(),_Directives(),_Functions(),_Labels());
}
