#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink

#Const C "ChaseTeams"

Text _Directives(){
	return """//!
#Struct K_{{{C}}} {
	Integer MapPoints;
	Integer RoundPoints;
	Integer RelayPoints;
	Text RelayerLogin;
	Integer CurrentCp;
	Integer LastSuccessfulRelayCp;
	Integer CpCrossCount;
	Boolean RelayStarted;
}
""";
}

Text _Functions(){
	return """//!
declare K_{{{C}}}[] G_{{{C}}};
Boolean {{{C}}}_CheckPlayer(CSmPlayer _Player){
	return _Player != Null && (_Player.CurrentClan == 1 || _Player.CurrentClan == 2);
}
""";
}

Text _Labels(){
	return """//!
***Init***
***
	G_{{{C}}} = [K_{{{C}}}{},K_{{{C}}}{}];
	declare netread Integer Net_{{{C}}}Change for Teams[0];
	declare Integer Prev{{{C}}}Change;
	declare netread K_{{{C}}}[] Net_{{{C}}} for Teams[0];
***

***Yield***
***
	if(Prev{{{C}}}Change != Net_{{{C}}}Change){
		Prev{{{C}}}Change = Net_{{{C}}}Change;
		G_{{{C}}} = Net_{{{C}}};
		+++{{{C}}}_Change+++
	}
***
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Manialink::CreateComponent(C,[],"",_Directives(),_Functions(),_Labels());
}
