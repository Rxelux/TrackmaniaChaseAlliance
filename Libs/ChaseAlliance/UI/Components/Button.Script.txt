#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink
#Include "ChaseAlliance/Libs/ChaseAlliance/Consts/Colors.Script.txt" as Colors
#Include "ChaseAlliance/Libs/Common/UI/Consts/Align.Script.txt" as Align
#Include "ChaseAlliance/Libs/Common/UI/Components/Common.Script.txt" as Common
#Include "ChaseAlliance/Libs/Common/UI/Components/Anims.Script.txt" as Anims

#Const C "Button"
#Const C_DefaultSize <50.,10.>
#Const C_DefaultPos <0.,0.>
#Const C_DefaultGrow <20.,2.>
#Const C_DefaultMove <50.,0.>

Text _Model(){
	declare Text IdleColor = Colors::ToHex6(Colors::C_AllianceDark);
	declare Text FocusColor = Colors::ToHex6(Colors::C_AllianceLightGreen);

	return """
<framemodel id="{{{C}}}" class="{{{C}}}" {{{Align::CC}}}>
	<quad id="{{{C}}}_button" bgcolor="{{{IdleColor}}}" bgcolorfocus="{{{FocusColor}}}" scriptevents="1" {{{Align::CC}}}/>
	<label id="text" textfont="RajdhaniMono" textsize="3" {{{Align::C2}}}/>
</framemodel>
""";
}

Text _Directives(){
	return """//!
#Struct K_{{{C}}}{
	CMlFrame Root;
	CMlLabel Label;
	CMlQuad Quad;
	Vec2 Size;
	Vec2 Pos;
	Vec2 Grow;
	Vec2 Move;
}
""";
}

Text _Functions(){
	return """//!

K_{{{C}}} {{{C}}}_GetFromRoot(CMlFrame _Root) {
	declare CMlLabel Label <=> (_Root.GetFirstChild("text") as CMlLabel);
	declare CMlQuad Quad <=> (_Root.GetFirstChild("{{{C}}}_button") as CMlQuad);
	return K_{{{C}}}{
		Root = _Root,
		Label = Label,
		Quad = Quad,
		Size = GetData(_Root,"size",{{{C_DefaultSize}}}),
		Pos = GetData(_Root,"pos",{{{C_DefaultPos}}}),
		Grow = GetData(_Root,"grow",{{{C_DefaultGrow}}}),
		Move = GetData(_Root,"move",{{{C_DefaultMove}}})
	};
}

K_{{{C}}} {{{C}}}_Get(CMlFrame _Parent,Text _Id) {
	declare CMlFrame Root <=> (_Parent.GetFirstChild(_Id) as CMlFrame);
	declare Button = {{{C}}}_GetFromRoot(Root);

	Root.RelativePosition_V3 = Button.Pos;
	Button.Quad.Size = Button.Size;
	Button.Label.Size = Button.Size;
	Button.Label.Value = GetData(Root,"text","Button");
	return Button;
}

Void {{{C}}}_In(K_{{{C}}} _Button){

	declare Size = _Button.Size;
	declare Grow = _Button.Grow;

	{{{Anims::C}}}_Animate(_Button.Label,"TextSize",0,"BackOut",_Button.Label.TextSizeReal,6.,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"SizeX",0,"BackOut",_Button.Quad.Size.X,Size.X+Grow.X,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"SizeY",0,"BackOut",_Button.Quad.Size.Y,Size.Y+Grow.Y,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"BgOpacity",0,"QuadOut",_Button.Quad.Opacity,1.,Now,200);

}

Void {{{C}}}_Out(K_{{{C}}} _Button){

	declare Size = _Button.Size;

	{{{Anims::C}}}_Animate(_Button.Label,"TextSize",0,"BackOut",_Button.Label.TextSizeReal,3.,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"SizeX",0,"BackOut",_Button.Quad.Size.X,Size.X,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"SizeY",0,"BackOut",_Button.Quad.Size.Y,Size.Y,Now,200);
	{{{Anims::C}}}_Animate(_Button.Quad,"BgOpacity",0,"QuadIn",_Button.Quad.Opacity,0.95,Now,200);

}

Void {{{C}}}_Enter(K_{{{C}}} _Button,Integer _StartTime){

	declare Pos = _Button.Pos;
	declare Move = _Button.Move;

	_Button.Root.RelativePosition_V3 = Pos-Move;
	_Button.Quad.Opacity = 0.0;
	_Button.Label.Opacity = 0.0;
	_Button.Quad.ScriptEvents_Restore();

	{{{Anims::C}}}_Animate(_Button.Root,"PosX",0,"BackOut",_Button.Root.RelativePosition_V3.X,Pos.X,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Root,"PosY",0,"BackOut",_Button.Root.RelativePosition_V3.Y,Pos.Y,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Label,"TextOpacity",0,"QuadOut",_Button.Label.Opacity,0.95,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Quad,"BgOpacity",0,"QuadOut",_Button.Quad.Opacity,0.95,_StartTime,400);
}

Void {{{C}}}_Exit(K_{{{C}}} _Button,Integer _StartTime){

	declare Pos = _Button.Pos;
	declare Move = _Button.Move;
	_Button.Quad.ScriptEvents_Disable();

	{{{Anims::C}}}_Animate(_Button.Root,"PosX",2,"BackOut",_Button.Root.RelativePosition_V3.X,Pos.X+Move.X,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Root,"PosY",2,"BackOut",_Button.Root.RelativePosition_V3.Y,Pos.Y+Move.Y,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Label,"TextOpacity",2,"QuadOut",_Button.Label.Opacity,0.,_StartTime,400);
	{{{Anims::C}}}_Animate(_Button.Quad,"BgOpacity",2,"QuadOut",_Button.Quad.Opacity,0.,_StartTime,400);
}

Void {{{C}}}_Handle(K_{{{C}}} _Button,CMlScriptEvent::Type _Type){
	if(_Type == CMlScriptEvent::Type::MouseOver){
		{{{C}}}_In(_Button);
	}
	if(_Type == CMlScriptEvent::Type::MouseOut){
		{{{C}}}_Out(_Button);
	}
}
""";
}

Text _Labels(){
	return """//!
***Event***
***
if (Event.ControlId == "{{{C}}}_button") {
	declare K_{{{C}}} Button = {{{C}}}_GetFromRoot(Event.Control.Parent);
	{{{C}}}_Handle(Button,Event.Type);
	+++{{{C}}}_Event+++
}
***
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Common::Load();
	Anims::Load();
	Manialink::CreateComponent(C,[Anims::C,"GetDataText","GetDataInteger","GetDataVec2"],_Model(),_Directives(),_Functions(),_Labels());
}