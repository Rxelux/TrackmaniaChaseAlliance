//#RequireContext CSmMode

#Include "ChaseAlliance/Libs/Common/Log.Script.txt"
#Include "ChaseAlliance/Libs/ChaseAlliance/Settings.Script.txt" as Settings

#Include "TextLib" as TL
#Include "ColorLib" as CL
#Include "MathLib" as ML

// #Const C_EventSource "ChaseTeams"
#Const C_JoinRankedGame "Ranked_JoinRankedGame"
#Const C_LeaveRankedGame "Ranked_LeaveRankedGame"
#Const C_StartRankedGame "Ranked_StartRankedGame"

#Struct K_ChaseTeamsPlayers {
	Text[] PlayersLogin;
	Text[] PlayersName;
    Boolean[] AskToStart;
}

declare K_ChaseTeamsPlayers[] G_ChaseTeamsPlayers;

K_ChaseTeamsPlayers GetTeam(Integer _Clan){
    return G_ChaseTeamsPlayers[_Clan-1];
}

K_ChaseTeamsPlayers GetOpposingTeam(Integer _Clan){
	declare Integer[] LUT = [1,0];
	return G_ChaseTeamsPlayers[LUT[_Clan-1]];
}

Integer RankedPlayerCount(){
    return G_ChaseTeamsPlayers[0].PlayersLogin.count + G_ChaseTeamsPlayers[1].PlayersLogin.count;
}


Integer RankedReadyPlayerCount(){
    declare Integer _RankedReadyPlayerCount = 0;
    foreach (AskToStart in G_ChaseTeamsPlayers[0].AskToStart) {
        if (AskToStart) _RankedReadyPlayerCount += 1;
    }
    foreach (AskToStart in G_ChaseTeamsPlayers[1].AskToStart) {
        if (AskToStart) _RankedReadyPlayerCount += 1;
    }
    return _RankedReadyPlayerCount;
}


// Void FillChaseTeamsPlayers(){
//     foreach(Player in Players){
//         if(Player != Null && Player.CurrentClan == 1){
//             G_ChaseTeamsPlayers[0].PlayersLogin.add(Player.User.Login);
//             G_ChaseTeamsPlayers[0].PlayersName.add(Player.User.Name);
//         }
//         else  if (Player != Null && Player.CurrentClan == 2){
//             G_ChaseTeamsPlayers[1].PlayersLogin.add(Player.User.Login);
//             G_ChaseTeamsPlayers[1].PlayersName.add(Player.User.Name);
//         }
//     }
//     Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
// }

Void _AddPlayerToRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (!G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.add(_Player.User.Login);
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersName.add(_Player.User.Name);
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.add(False);
            UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s joined a ranked game! ");
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
	        if(UI != Null) UI.SendChat("You're already in the ranked game in $"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^Teams[_Player.CurrentClan-1].Name);
        }
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}

Void _RemovePlayerFromRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
            if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart[G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.keyof(_Player.User.Login)] == False){
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.remove(False);
            }else{
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.remove(True);
            }
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.remove(_Player.User.Login);
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersName.remove(_Player.User.Name);
            
            UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s left the ranked game!");
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
	        if(UI != Null) UI.SendChat("You haven't joined a ranked game yet");
        }
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}

Boolean TeamsAreReady(){
    if (G_ChaseTeamsPlayers[0].PlayersLogin.count < 2 || G_ChaseTeamsPlayers[1].PlayersLogin.count < 2) return False;   
    return (G_ChaseTeamsPlayers[0].AskToStart.exists(False) || G_ChaseTeamsPlayers[1].AskToStart.exists(False)) && G_ChaseTeamsPlayers[0].AskToStart.count == G_ChaseTeamsPlayers[1].AskToStart.count;
}

Boolean PlayerIsInRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
            return True;
        }
    }
    return False;
}

Text RankedPlayersWithColor(){
    declare Text PlayersText = "";
    foreach(Player in Players){
        if (Player != Null && PlayerIsInRankedGame(Player)){
            PlayersText = PlayersText^"$"^TL::ColorToText(Teams[Player.CurrentClan-1].ColorUI)^Player.User.Name^", ";
        }
    }
    return PlayersText;
}

Void _AddPlayerAskToStart(CSmPlayer _Player){
    if (_Player != Null){ 
        if (PlayerIsInRankedGame(_Player)){
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart[G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.keyof(_Player.User.Login)] = True;
            declare CUIConfig UI = UIManager.GetUI(_Player);
            UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s is ready "^RankedReadyPlayerCount()^"/"^RankedPlayerCount());
            if (TeamsAreReady()){
                UIManager.UIAll.SendChat("All players are Ready, ranked game is starting, Good Luck Have Fun "^RankedPlayersWithColor()^"$z$s!");
                //TODO print current players points from http GET
            }
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
            if(UI != Null) UI.SendChat("You haven't joined a ranked game yet");
        }
        
        
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].AskToStart);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].AskToStart);
}

Void _ClearAll(){
    G_ChaseTeamsPlayers.clear();
    G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
	G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}


Boolean TeamsAreConsistant(){
    declare Integer ActivePlayerCount = 0;
    foreach(Player in Players){
        if(Player != Null && Player.CurrentClan == 1){
            ActivePlayerCount += 1;
            if (!G_ChaseTeamsPlayers[0].PlayersLogin.exists(Player.User.Login)){
                return False;
            }
        }
        else if (Player != Null && Player.CurrentClan == 2){
            ActivePlayerCount += 1;
            if (!G_ChaseTeamsPlayers[1].PlayersLogin.exists(Player.User.Login)){
                return False;
            }
        }
    }
    return G_ChaseTeamsPlayers[0].PlayersLogin.count + G_ChaseTeamsPlayers[1].PlayersLogin.count == ActivePlayerCount;
}

Void PassUIEvent(CUIConfigEvent _Event){
	if(_Event.Type != CUIConfigEvent::EType::OnLayerCustomEvent) return;
	if(_Event.UI == Null) return;
	
	if(_Event.CustomEventType == C_JoinRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_AddPlayerToRankedGame(Player);
	}
	if(_Event.CustomEventType == C_LeaveRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_RemovePlayerFromRankedGame(Player);
	}
	if(_Event.CustomEventType == C_StartRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_AddPlayerAskToStart(Player);
	}
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
    G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
	G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	// Messages::Load();
	// Events::Load();
	// GameState::Load();
	// Commands::Load();
}




