//#RequireContext CSmMode

#Include "ChaseAlliance/Libs/Common/Log.Script.txt"
// #Include "ChaseAlliance/Libs/ChaseAlliance/Players.Script.txt" as PlayersL
#Include "ChaseAlliance/Libs/ChaseAlliance/HttpRequests.Script.txt" as HttpRequests
#Include "ChaseAlliance/Libs/ChaseAlliance/Settings.Script.txt" as Settings
#Include "ChaseAlliance/Libs/ChaseAlliance/Consts/Colors.Script.txt" as Colors

#Include "TextLib" as TL

#Const C_JoinOrLeaveRankedGame "Ranked_JoinOrLeaveRankedGame"
#Const C_ReadyRankedGame "Ranked_ReadyRankedGame"
#Const C_AddBotsRankedGame "Ranked_AddBotsRankedGame"

#Struct K_ChaseTeamsPlayers {
	Text[] PlayersLogin;
	Text[] PlayersName;
    Boolean[] AskToStart;
}

declare K_ChaseTeamsPlayers[] G_ChaseTeamsPlayers;

K_ChaseTeamsPlayers GetTeam(Integer _Clan){
    return G_ChaseTeamsPlayers[_Clan-1];
}

K_ChaseTeamsPlayers GetOpposingTeam(Integer _Clan){
	declare Integer[] LUT = [1,0];
	return G_ChaseTeamsPlayers[LUT[_Clan-1]];
}

Void _SetSpectator(CSmPlayer _Player){
	if(_Player.SpawnStatus != CSmPlayer::ESpawnStatus::NotSpawned){
		UnspawnPlayer(_Player);
	}
	ServerAdmin.ForceSpectator(_Player.User, CServerAdmin::ESpecMode::SpectatorSelectable);
}

Integer RankedReadyPlayerCount(){
    declare Integer _RankedReadyPlayerCount = 0;
    foreach (AskToStart in G_ChaseTeamsPlayers[0].AskToStart) {
        if (AskToStart) _RankedReadyPlayerCount += 1;
    }
    foreach (AskToStart in G_ChaseTeamsPlayers[1].AskToStart) {
        if (AskToStart) _RankedReadyPlayerCount += 1;
    }
    return _RankedReadyPlayerCount;
}

Integer RankedPlayerCount(){
    return G_ChaseTeamsPlayers[0].PlayersLogin.count + G_ChaseTeamsPlayers[1].PlayersLogin.count;
}

Void _AddPlayerToRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (!G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.add(_Player.User.Login);
            if (TL::StartsWith("FakeUser#" , _Player.User.Name)){
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersName.add("FakePlayer");
            }
            else{
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersName.add(_Player.User.Name);
            }
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.add(False);
            declare Integer RankedPlayerCount = RankedPlayerCount();
            if (RankedPlayerCount == 1){
                UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s joined a ranked game! $"^TL::ColorToText(Colors::C_Success)^RankedPlayerCount^"$z$s player in.");
            }else{
                UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s joined a ranked game! $"^TL::ColorToText(Colors::C_Success)^RankedPlayerCount^"$z$s players in.");
            }
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
	        if(UI != Null) UI.SendChat("You're already in the ranked game in $"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^Teams[_Player.CurrentClan-1].Name);
        }
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}

Boolean TeamIsFull(Integer _TeamId){
    return (G_ChaseTeamsPlayers[_TeamId].PlayersLogin.count == 3);
}

Void _RemovePlayerFromRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (!TeamIsFull(_Player.CurrentClan-1)){
            if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
                if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart[G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.keyof(_Player.User.Login)] == False){
                    G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.remove(False);
                }else{
                    G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart.remove(True);
                }
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.remove(_Player.User.Login);
                G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersName.remove(_Player.User.Name);
                
                UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s left the ranked game!");
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
            if(UI != Null) UI.SendChat("Ranked mode only support teams of <= 3 players for now");
        }

        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
	        if(UI != Null) UI.SendChat("You haven't joined a ranked game yet");
        }
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}

Boolean TeamsAreReady(){
    if (G_ChaseTeamsPlayers[0].PlayersLogin.count < 2 || G_ChaseTeamsPlayers[1].PlayersLogin.count < 2) return False;   
    return G_ChaseTeamsPlayers[0].AskToStart.containsonly([True]) && G_ChaseTeamsPlayers[1].AskToStart.containsonly([True]) && G_ChaseTeamsPlayers[0].AskToStart.count == G_ChaseTeamsPlayers[1].AskToStart.count;
}

Boolean PlayerIsInRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.exists(_Player.User.Login)){
            return True;
        }
    }
    return False;
}

Text RankedPlayersWithColor(){
    declare Text PlayersText = "";
    foreach(Player in Players){
        if (Player != Null && PlayerIsInRankedGame(Player)){
            PlayersText = PlayersText^"$"^TL::ColorToText(Teams[Player.CurrentClan-1].ColorUI)^Player.User.Name^" ";
        }
    }
    return PlayersText;
}

Void _AddPlayerAskToStart(CSmPlayer _Player){
    if (_Player != Null){ 
        if (PlayerIsInRankedGame(_Player)){
            G_ChaseTeamsPlayers[_Player.CurrentClan-1].AskToStart[G_ChaseTeamsPlayers[_Player.CurrentClan-1].PlayersLogin.keyof(_Player.User.Login)] = True;
            UIManager.UIAll.SendChat("$"^TL::ColorToText(Teams[_Player.CurrentClan-1].ColorUI)^_Player.User.Name^"$z$s is ready "^RankedReadyPlayerCount()^"/"^RankedPlayerCount());
            if (TeamsAreReady()){
                UIManager.UIAll.SendChat("All players are Ready, ranked game is starting! Good Luck Have Fun "^RankedPlayersWithColor()^"$z$s!");
                UIManager.UIAll.SendChat(HttpRequests::GetRankedPlayersStats(G_ChaseTeamsPlayers[0].PlayersLogin,G_ChaseTeamsPlayers[0].PlayersName, G_ChaseTeamsPlayers[1].PlayersLogin, G_ChaseTeamsPlayers[1].PlayersName));
                foreach (Player in Players){
                    if (Player != Null && !PlayerIsInRankedGame(Player)){
                        _SetSpectator(Player);
                    }
                }
            }
        }else{
            declare CUIConfig UI = UIManager.GetUI(_Player);
            if(UI != Null) UI.SendChat("You haven't joined a ranked game yet");
        }
        
        
    }
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].AskToStart);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].AskToStart);
}

Void _AddOrRemovePlayerToRankedGame(CSmPlayer _Player){
    if (_Player != Null){
        if (PlayerIsInRankedGame(_Player)){
            _RemovePlayerFromRankedGame(_Player);
        }else{
            _AddPlayerToRankedGame(_Player);
        }
    }
}

Void _AddBotsToRanked(CSmPlayer _Player){
    foreach (Player in Players) {
        if (Player != _Player){
            _AddOrRemovePlayerToRankedGame(Player);
            _AddPlayerAskToStart(Player);
        }
    }
}

Void _ClearAll(){
    G_ChaseTeamsPlayers.clear();
    G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
	G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
    Log("Team 1 : "^ G_ChaseTeamsPlayers[0].PlayersName);
    Log("Team 2 : "^ G_ChaseTeamsPlayers[1].PlayersName);
}


Boolean TeamsAreConsistant(){
    declare Integer ActivePlayerCount = 0;
    foreach(Player in Players){
        if(Player != Null && Player.CurrentClan == 1){
            ActivePlayerCount += 1;
            if (!G_ChaseTeamsPlayers[0].PlayersLogin.exists(Player.User.Login)){
                return False;
            }
        }
        else if (Player != Null && Player.CurrentClan == 2){
            ActivePlayerCount += 1;
            if (!G_ChaseTeamsPlayers[1].PlayersLogin.exists(Player.User.Login)){
                return False;
            }
        }
    }
    return G_ChaseTeamsPlayers[0].PlayersLogin.count + G_ChaseTeamsPlayers[1].PlayersLogin.count == ActivePlayerCount;
}

Void PassUIEvent(CUIConfigEvent _Event){
    if (_Event == Null) return;
    if(_Event.Type != CUIConfigEvent::EType::OnLayerCustomEvent) return;
	if( _Event.UI == Null) return;
	
	if(_Event.CustomEventType == C_JoinOrLeaveRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_AddOrRemovePlayerToRankedGame(Player);
	}
	if(_Event.CustomEventType == C_ReadyRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_AddPlayerAskToStart(Player);
	}
	if(_Event.CustomEventType == C_AddBotsRankedGame){
		declare Player = GetPlayer(_Event.UI);
		_AddBotsToRanked(Player);
	}
}

Void PassModeEvent(CSmModeEvent _Event, Text _HttpApiURL){
	if (_Event.Type == CSmModeEvent::EType::OnPlayerRemoved){
        if(_Event.Player == Null) return;
        declare CSmPlayer Player = _Event.Player;
		if (Settings::GetRankedMode() == 1 && PlayerIsInRankedGame(Player)){
            _RemovePlayerFromRankedGame(Player);
        }
	}
    if (_Event.Type == CSmModeEvent::EType::OnPlayerAdded){
        if(_Event.Player == Null) return;
        declare CSmPlayer Player = _Event.Player;
		if (Settings::GetRankedMode() == 1 && !PlayerIsInRankedGame(Player)){
            _SetSpectator(Player);
            declare CUIConfig UI = UIManager.GetUI(Player);
            if(UI != Null) UI.SendChat("A ranked game is in progress, please wait for it to finish before joining");
        }
	}
}


declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
    G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
	G_ChaseTeamsPlayers.add(K_ChaseTeamsPlayers{});
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	// Messages::Load();
	// Events::Load();
	// GameState::Load();
	// Commands::Load();
}




