//#RequireContext CSmMode
#Include "ChaseAlliance/Libs/Common/Log.Script.txt"
#Include "ChaseAlliance/Libs/Common/Messages.Script.txt" as Messages
#Include "ChaseAlliance/Libs/Common/Events.Script.txt" as Events
#Include "ChaseAlliance/Libs/ModeBase/GameLoopState.Script.txt" as GameLoop
#Include "ChaseAlliance/Libs/ChaseAlliance/GameState.Script.txt" as GameState
#Include "ChaseAlliance/Libs/ChaseAlliance/Consts/Colors.Script.txt" as Colors
#Include "ChaseAlliance/Libs/ChaseAlliance/Settings.Script.txt" as Settings

#Include "TextLib" as TL

#Const C_EventSource "ChaseTeams"

#Const C_WinByKo 0
#Const C_WinByFinish 1
#Const C_WinByForfeit 2

#Struct K_ChaseTeams {
	Integer MapPoints;
	Integer RoundPoints;
	Integer RelayPoints;
	Text RelayerLogin;
	Integer CurrentCp;
	Integer LastSuccessfulRelayCp;
	Integer CpCrossCount;
	Boolean RelayStarted;
}

declare K_ChaseTeams[] G_ChaseTeams;

declare Integer G_WinState;
declare Integer G_WinClan;

K_ChaseTeams GetTeam(Integer _Clan){
	return G_ChaseTeams[_Clan-1];
}

K_ChaseTeams GetOpposingTeam(Integer _Clan){
	declare Integer[] LUT = [1,0];
	return G_ChaseTeams[LUT[_Clan-1]];
}

Integer GetWinState(){
	return G_WinState;
}

Text GetWinStateString(){
	switch (G_WinState){
		case C_WinByFinish: return "Win by finish";
		case C_WinByForfeit: return "Win by forfeit";
		case C_WinByKo: return "Win by ko";
	}
	return "";
}

Integer GetWinClan(){
	return G_WinClan;
}

Void ToNet(){
	declare netwrite K_ChaseTeams[] Net_ChaseTeams for Teams[0] = G_ChaseTeams;
	declare netwrite Integer Net_ChaseTeamsChange for Teams[0] = Now;
	Net_ChaseTeams = G_ChaseTeams;
	Net_ChaseTeamsChange = Now;
}

Void _SetTeamMapPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].MapPoints = _Value;
}

Void _SetTeamRoundPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].RoundPoints = _Value;
}

Void _SetTeamRelayPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].RelayPoints = _Value;
}

Void _SetTeamCurrentCp(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].CurrentCp = _Value;
}

Void _SetTeamLastSuccessfulRelayCp(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].LastSuccessfulRelayCp= _Value;
}

Void _SetTeamCpCrossCount(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].CpCrossCount = _Value;
}

Void _SetTeamRelayerLogin(Integer _Clan,Text _Value){
	G_ChaseTeams[_Clan-1].RelayerLogin = _Value;
}

Void _SetTeamRelayStarted(Integer _Clan,Boolean _Value){
	G_ChaseTeams[_Clan-1].RelayStarted = _Value;
}

Void _IncrementTeamRelayPoint(Integer _Clan){
	_SetTeamRelayPoint(_Clan,G_ChaseTeams[_Clan-1].RelayPoints + 1);
}

Void _ResetTeamRelay(Integer _Clan){
	_SetTeamRelayPoint(_Clan,0);
	_SetTeamCurrentCp(_Clan,0);
	_SetTeamLastSuccessfulRelayCp(_Clan,0);
	_SetTeamCpCrossCount(_Clan,0);
	_SetTeamRelayerLogin(_Clan,"");
	_SetTeamRelayStarted(_Clan,False);
	G_WinState = -1;
	G_WinClan = -1;
}

Void IncrementTeamRoundPoint(Integer _Clan){
	_SetTeamRoundPoint(_Clan,G_ChaseTeams[_Clan-1].RoundPoints + 1);
}

Integer GetTeamRoundPoint(Integer _Clan){
	return G_ChaseTeams[_Clan-1].RoundPoints;
}

Void _ResetTeamRoundPoint(Integer _Clan){
	_SetTeamRoundPoint(_Clan,0);
}

Void ResetTeamsRoundPoint(){
	_ResetTeamRoundPoint(1);
	_ResetTeamRoundPoint(2);
}

Void IncrementTeamMapPoint(Integer _Clan){
	_SetTeamMapPoint(_Clan,G_ChaseTeams[_Clan-1].MapPoints + 1);
}

Integer GetTeamMapPoint(Integer _Clan){
	return G_ChaseTeams[_Clan-1].MapPoints;
}

Void _ResetTeamMapPoint(Integer _Clan){
	_SetTeamMapPoint(_Clan,0);
}

Void ResetTeamsMapPoint(){
	_ResetTeamMapPoint(1);
	_ResetTeamMapPoint(2);
}

Void ResetTeamsRelay(){
	_ResetTeamRelay(1);
	_ResetTeamRelay(2);
	Events::RemoveAllSortedWithSource(C_EventSource);
}

Void ResetChaseTeams(){
	ResetTeamsMapPoint();
	ResetTeamsRoundPoint();
	ResetTeamsRelay();
}

Boolean IsComplete(Integer _Clan){
	return ClansNbPlayers[_Clan] >= Settings::GetRelayerCount();
}

Boolean AreComplete(){
	return IsComplete(1) && IsComplete(2);
}

Void PassModeEvent(CSmModeEvent _Event){
	if(G_WinState >= 0) return;
	if(GameState::IsPlaying() && _Event.Type == CSmModeEvent::EType::OnPlayerTriggersWaypoint){
		if(_Event.Player == Null) return;
		if(_Event.Player.CurrentClan != 1 && _Event.Player.CurrentClan != 2)return;
		//add event to sorted event

		Events::CreateSorted(
			_Event.WaypointTime,
			Settings::GetRelaySortDelay(),
			"WaypointTrigger",
			C_EventSource,
			[_Event.Player.User.Login],
			[_Event.Player.CurrentClan,_Event.Player.RaceWaypointTimes.count],
			[],
			[_Event.IsFinish]);
	}
}

Void PassLibSortedEvent(Integer _EventsTime, Events::K_SortedEvent[] _Events){

	//here event are in order
	declare Boolean NeedUpdateNet = False;
	foreach (Event in _Events) {
		if(G_WinState >= 0) continue;

		//since we now have all the events that occured at the same time we can use this to make sure that
		//in case of a tie between relayer and chaser the relay is always successful and not random based on ping
		//and that in case of a finsh and ko the finish has priority

		if(Event.Source != C_EventSource) continue;
		declare PlayerLogin = Event.Texts[0];
		declare CSmPlayer Player = GetPlayer(PlayerLogin);
		//check if the player is still on the server since the event was added in the sort buffer
		if(Player == Null) continue;
		NeedUpdateNet = True;

		//even tho we have the Player class we'll still use the old state of this player in case it changed since the event was added in the sort buffer
		//for exemple player could cross 2 cp in a time < sort buffer time
		declare CurrentClan = Event.Integers[0];
		declare RaceWaypoint = Event.Integers[1];
		declare IsFinish = Event.Booleans[0];

		if(IsFinish){
			G_WinState = C_WinByFinish;
			G_WinClan = CurrentClan;
		}

		declare K_ChaseTeams Team = GetTeam(CurrentClan);
		
		if(RaceWaypoint > Team.CurrentCp){ //crossed a new cp
			// Log("Crossed a new cp");
			
			if(Team.RelayerLogin == Player.User.Login){//check if player is relayer
				// Log("Player was relayer");
				//relay passed
				// Log("Relay success");
				
				declare K_ChaseTeams OpposingTeam = GetOpposingTeam(CurrentClan);
				_IncrementTeamRelayPoint(CurrentClan);
				_SetTeamRelayerLogin(CurrentClan,"");//reset relayer
				_SetTeamLastSuccessfulRelayCp(CurrentClan,RaceWaypoint);
				if((Team.RelayPoints+1) - OpposingTeam.RelayPoints >= Settings::GetRelayGap()){
					G_WinState = C_WinByKo;
					G_WinClan = CurrentClan;
				}else{
					Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Success)^"Relay Success",3000,CUIConfig::EUISound::ScoreProgress);
					Messages::SendStatus(CurrentClan,"",0,CUIConfig::EUISound::Silence);
					Messages::SendStatus(Player,"Wait for players",3000,CUIConfig::EUISound::Silence);
				}

			}else{
				// Log("Player was Not relayer");
				if(Team.RelayStarted){
					//relay failed
					// Log("Relay failed");
					Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Failure)^"Relay Failed",3000,CUIConfig::EUISound::FirstHit);
					if(Team.RelayerLogin == ""){
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Failure)^"Wait for players",3000,CUIConfig::EUISound::Silence);
					}else{
						declare CSmPlayer RelayerPlayer = GetPlayer(Team.RelayerLogin);
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Failure)^"Wait for "^RelayerPlayer.User.Name,3000,CUIConfig::EUISound::Silence);
					}
				}else{
					//first cp crossed
					// Log("First cp crossed");
					Messages::SendStatus(Player,"Wait for players",3000,CUIConfig::EUISound::Silence);
					_SetTeamRelayStarted(CurrentClan, True);
				}
			}
			_SetTeamCurrentCp(CurrentClan,RaceWaypoint);
			_SetTeamCpCrossCount(CurrentClan,1);
		} 
		else if (RaceWaypoint > Team.LastSuccessfulRelayCp){
			if(Team.RelayerLogin != PlayerLogin){//check if player is not relayer
				// Log("Player was Not relayer2");
				if(Team.RelayStarted){
					//relay failed
					// Log("Relay failed2");
					if(Team.RelayerLogin == ""){
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Failure)^"Wait for players",3000,CUIConfig::EUISound::Silence);
					}else{
						declare CSmPlayer RelayerPlayer = GetPlayer(Team.RelayerLogin);
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Failure)^"Wait for "^RelayerPlayer.User.Name,3000,CUIConfig::EUISound::Silence);
					}
				}
			}
		}
		if(RaceWaypoint == Team.CurrentCp){ //crossed the current cp
			// Log("Crossed current cp");
			if(Team.CpCrossCount == Settings::GetRelayerCount()-1){//current cp was crossed by all team but this player
				// Log("Current cp was crossed by all team");
				if(Team.RelayerLogin == ""){
					_SetTeamRelayerLogin(CurrentClan, PlayerLogin);

					if(Team.LastSuccessfulRelayCp == Team.CurrentCp || Team.CurrentCp == 1){
						Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Success)^""^Player.User.Name,3000,CUIConfig::EUISound::Warning);
					}else{
						Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Failure)^""^Player.User.Name,3000,CUIConfig::EUISound::Warning);
					}
					Messages::SendStatus(CurrentClan,"Next Checkpoint",3000,CUIConfig::EUISound::Silence);
				}
			}
			// else{
				// Log("Current cp was Not crossed by all team");
			// }
			_SetTeamCpCrossCount(CurrentClan, Team.CpCrossCount + 1);
		}
		// if(RaceWaypoint < Team.CurrentCp){
			// Log("Crossed a previous cp");
		// }
	}
	if(NeedUpdateNet){
		ToNet();
	}
}

Void Yield(){

	if(!GameState::IsPlaying() || G_WinState >= 0) return;

	if(!IsComplete(1)){
		G_WinState = C_WinByForfeit;
		G_WinClan = 2;
		// Log("Playing -> Team 1 is Not complete");
	}
	if(!IsComplete(2)){
		G_WinState = C_WinByForfeit;
		G_WinClan = 1;
		// Log("Waiting -> Team2 is Not complete");
	}
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
	G_ChaseTeams.add(K_ChaseTeams{});
	G_ChaseTeams.add(K_ChaseTeams{});
	Teams[0].ColorUI = Colors::C_Team0;
	Teams[0].Name = "Team Azure";
	Teams[1].ColorUI = Colors::C_Team1;
	Teams[1].Name = "Team Orange";
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Messages::Load();
	Events::Load();
	GameState::Load();
	GameLoop::Load();
}