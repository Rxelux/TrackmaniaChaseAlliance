//#RequireContext CSmMode
#Include "ChaseAlliance/Libs/Common/Log.Script.txt"
#Include "ChaseAlliance/Libs/Common/Messages.Script.txt" as Messages
#Include "ChaseAlliance/Libs/Common/Events.Script.txt" as Events
#Include "ChaseAlliance/Libs/ModeBase/GameLoopState.Script.txt" as GameLoop
#Include "ChaseAlliance/Libs/ChaseAlliance/GameState.Script.txt" as GameState
#Include "ChaseAlliance/Libs/ChaseAlliance/Consts/Colors.Script.txt" as Colors
#Include "TextLib" as TL

#Const C_EventSource "ChaseTeams"

#Struct K_ChaseTeams {
	Integer MapPoints;
	Integer RoundPoints;
	Integer RelayPoints;
	Text RelayerLogin;
	Integer CurrentCp;
	Integer LastSuccessfulRelayCp;
	Integer CpCrossCount;
	Boolean RelayStarted;
}

declare K_ChaseTeams[] G_ChaseTeams;

declare Integer G_RelayerCount;
declare Integer G_RelayGap;
declare Integer G_RelaySortDelay;

Void SetRelayerCount(Integer _Count){
	G_RelayerCount = _Count;
}

Void SetRelayGap(Integer _Count){
	G_RelayGap = _Count;
}

Void SetRelaySortDelay(Integer _Delay){
	G_RelaySortDelay = _Delay;
}

Void ToNet(){
	declare netwrite K_ChaseTeams[] Net_ChaseTeams for Teams[0] = G_ChaseTeams;
	declare netwrite Integer Net_ChaseTeamsChange for Teams[0] = Now;
	Net_ChaseTeams = G_ChaseTeams;
	Net_ChaseTeamsChange = Now;
}

Void _SetTeamMapPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].MapPoints = _Value;
}

Void _SetTeamRoundPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].RoundPoints = _Value;
}

Void _SetTeamRelayPoint(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].RelayPoints = _Value;
}

Void _SetTeamCurrentCp(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].CurrentCp = _Value;
}

Void _SetTeamLastSuccessfulRelayCp(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].LastSuccessfulRelayCp= _Value;
}

Void _SetTeamCpCrossCount(Integer _Clan,Integer _Value){
	G_ChaseTeams[_Clan-1].CpCrossCount = _Value;
}

Void _SetTeamRelayerLogin(Integer _Clan,Text _Value){
	G_ChaseTeams[_Clan-1].RelayerLogin = _Value;
}

Void _SetTeamRelayStarted(Integer _Clan,Boolean _Value){
	G_ChaseTeams[_Clan-1].RelayStarted = _Value;
}

Void _IncrementTeamRelayPoint(Integer _Clan){
	_SetTeamRelayPoint(_Clan,G_ChaseTeams[_Clan-1].RelayPoints + 1);
}

Void _ResetTeamRelay(Integer _Clan){
	_SetTeamRelayPoint(_Clan,0);
	_SetTeamCurrentCp(_Clan,0);
	_SetTeamLastSuccessfulRelayCp(_Clan,0);
	_SetTeamCpCrossCount(_Clan,0);
	_SetTeamRelayerLogin(_Clan,"");
	_SetTeamRelayStarted(_Clan,False);
}

Void IncrementTeamRoundPoint(Integer _Clan){
	_SetTeamRoundPoint(_Clan,G_ChaseTeams[_Clan-1].RoundPoints + 1);
}

Void _ResetTeamRoundPoint(Integer _Clan){
	_SetTeamRoundPoint(_Clan,0);
}

Void ResetTeamsRoundPoint(){
	_ResetTeamRoundPoint(1);
	_ResetTeamRoundPoint(2);
}

Void IncrementTeamMapPoint(Integer _Clan){
	_SetTeamMapPoint(_Clan,G_ChaseTeams[_Clan-1].MapPoints + 1);
}

Void _ResetTeamMapPoint(Integer _Clan){
	_SetTeamMapPoint(_Clan,0);
}

Void ResetTeamsMapPoint(){
	_ResetTeamMapPoint(1);
	_ResetTeamMapPoint(2);
}

Void ResetTeamsRelay(){
	_ResetTeamRelay(1);
	_ResetTeamRelay(2);
	Events::RemoveAllSortedWithSource(C_EventSource);
	Messages::ClearAll();
}

Void ResetChaseTeams(){
	ResetTeamsMapPoint();
	ResetTeamsRoundPoint();
	ResetTeamsRelay();
}

Boolean IsComplete(Integer _Clan){
	return ClansNbPlayers[_Clan] >= G_RelayerCount;
}

Boolean AreComplete(){
	return IsComplete(1) && IsComplete(2);
}

Void PassModeEvent(CSmModeEvent _Event){
	if(GameState::IsPlaying() && _Event.Type == CSmModeEvent::EType::OnPlayerTriggersWaypoint){
		if(_Event.Player == Null) return;
		if(_Event.Player.CurrentClan != 1 && _Event.Player.CurrentClan != 2)return;
		//add event to sorted event

		Events::CreateSorted(
			_Event.WaypointTime,
			"WaypointTrigger",
			C_EventSource,
			[_Event.Player.User.Login],
			[_Event.Player.CurrentClan,_Event.Player.RaceWaypointTimes.count],
			[],[]);
	}
}

Void PassLibSortedEvent(Integer _EventsTime, Events::K_SortedEvent[] _Events){

	//here event are in order
	declare Boolean NeedUpdateNet = False;
	foreach (Event in _Events) {

		//since we now have all the events that occured at the same time we can use this to make sure that
		//in case of a tie between relayer and chaser the relay is always successful and not random based on ping
		//and that in case of a finsh and ko the finish has priority

		if(Event.Source != C_EventSource) continue;
		declare PlayerLogin = Event.Texts[0];
		declare CSmPlayer Player = GetPlayer(PlayerLogin);
		//check if the player is still on the server since the event was added in the sort buffer
		if(Player == Null) continue;
		NeedUpdateNet = True;

		//even tho we have the Player class we'll still use the old state of this player in case it changed since the event was added in the sort buffer
		//for exemple player could cross 2 cp in a time < sort buffer time
		declare CurrentClan = Event.Integers[0];
		declare RaceWaypoint = Event.Integers[1];

		declare K_ChaseTeams Team = G_ChaseTeams[CurrentClan-1];

		if(RaceWaypoint > Team.CurrentCp){ //crossed a new cp
			Log("Crossed a new cp");
			
			if(Team.RelayerLogin == Player.User.Login){//check if player is relayer
				Log("Player was relayer");
				//relay passed
				Log("Relay success");
				_IncrementTeamRelayPoint(CurrentClan);
				_SetTeamRelayerLogin(CurrentClan,"");//reset relayer
				_SetTeamLastSuccessfulRelayCp(CurrentClan,RaceWaypoint);
				Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Success)^"Relay Success",3000,CUIConfig::EUISound::ScoreProgress);
				Messages::SendStatus(CurrentClan,"",0,CUIConfig::EUISound::Silence);
				Messages::SendStatus(Player,"Wait for players",3000,CUIConfig::EUISound::Silence);

			}else{
				Log("Player was Not relayer");
				if(Team.RelayStarted){
					//relay failed
					Log("Relay failed");
					Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Error)^"Relay Failed",3000,CUIConfig::EUISound::FirstHit);
					if(Team.RelayerLogin == ""){
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Error)^"Wait for players",3000,CUIConfig::EUISound::Silence);
					}else{
						declare CSmPlayer RelayerPlayer = GetPlayer(Team.RelayerLogin);
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Error)^"Wait for "^RelayerPlayer.User.Name,3000,CUIConfig::EUISound::Silence);
					}
				}else{
					//first cp crossed
					Log("First cp crossed");
					Messages::SendStatus(Player,"Wait for players",3000,CUIConfig::EUISound::Silence);
					_SetTeamRelayStarted(CurrentClan, True);
				}
			}
			_SetTeamCurrentCp(CurrentClan,RaceWaypoint);
			_SetTeamCpCrossCount(CurrentClan,1);
		} 
		else if (RaceWaypoint > Team.LastSuccessfulRelayCp){
			if(Team.RelayerLogin != PlayerLogin){//check if player is not relayer
				Log("Player was Not relayer2");
				if(Team.RelayStarted){
					//relay failed
					Log("Relay failed2");
					if(Team.RelayerLogin == ""){
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Error)^"Wait for players",3000,CUIConfig::EUISound::Silence);
					}else{
						declare CSmPlayer RelayerPlayer = GetPlayer(Team.RelayerLogin);
						Messages::SendStatus(Player,"$"^TL::ColorToText(Colors::C_Error)^"Wait for "^RelayerPlayer.User.Name,3000,CUIConfig::EUISound::Silence);
					}
				}
			}
		}
		if(RaceWaypoint == Team.CurrentCp){ //crossed the current cp
			Log("Crossed current cp");
			if(Team.CpCrossCount == G_RelayerCount-1){//current cp was crossed by all team but this player
				Log("Current cp was crossed by all team");
				if(Team.RelayerLogin == ""){
					_SetTeamRelayerLogin(CurrentClan, PlayerLogin);

					if(Team.LastSuccessfulRelayCp == Team.CurrentCp-1){
						Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Success)^""^Player.User.Name,3000,CUIConfig::EUISound::Warning);
					}else{
						Messages::SendBig(CurrentClan,"$"^TL::ColorToText(Colors::C_Error)^""^Player.User.Name,3000,CUIConfig::EUISound::Warning);
					}
					Messages::SendStatus(CurrentClan,"Next Checkpoint",3000,CUIConfig::EUISound::Silence);
				}
			}else{
				Log("Current cp was Not crossed by all team");
			}
			_SetTeamCpCrossCount(CurrentClan, Team.CpCrossCount + 1);
		}
		if(RaceWaypoint < Team.CurrentCp){
			Log("Crossed a previous cp");
		}
	}
	if(NeedUpdateNet){
		ToNet();
	}
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
	G_ChaseTeams.add(K_ChaseTeams{});
	G_ChaseTeams.add(K_ChaseTeams{});
	Teams[0].ColorPrimary = Colors::C_Team0Primary;
	Teams[0].ColorSecondary = Colors::C_Team0Secondary;
	Teams[0].ColorUI = Colors::C_Team0Primary;
	Teams[0].Name = "Team Azure";
	Teams[1].ColorPrimary = Colors::C_Team1Primary;
	Teams[1].ColorSecondary = Colors::C_Team1Secondary;
	Teams[1].ColorUI = Colors::C_Team1Primary;
	Teams[1].Name = "Team Orange";
	G_RelayerCount = 3;
	G_RelayGap = 3;
	G_RelaySortDelay = 250;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Messages::Load();
	Events::Load();
	GameState::Load();
	GameLoop::Load();
}