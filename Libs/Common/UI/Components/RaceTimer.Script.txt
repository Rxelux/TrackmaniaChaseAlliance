//#RequireContext CSmMlScriptIngame
#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink
#Include "ChaseAlliance/Libs/Common/UI/Consts/Align.Script.txt" as Align
#Include "TextLib" as TL

#Const C "RaceTimer"

Text _Model (){
	return """
<framemodel id="{{{C}}}" class="{{{C}}}" {{{Align::CB}}}>
	<frame id="frame" hidden="1">
		<label id="race-timer" pos="0 -1.6" size="64 8" textfont="RajdhaniMono" textsize="8" textemboss="1" {{{Align::CB}}}/>
	</frame>
</framemodel>
""";
}

Text _Directives(){
	return """//!
#Struct K_{{{C}}}{
	CMlFrame Root;
	CMlFrame Frame;
	CMlLabel RaceTimer;
}
""";
}

Text _Functions(){
	return """//!
K_{{{C}}} {{{C}}}_Get(CMlFrame _Parent,Text _Id) {
	declare CMlFrame Root <=> (_Parent.GetFirstChild(_Id) as CMlFrame);
	declare CMlFrame Frame <=> (Root.GetFirstChild("frame") as CMlFrame);
	declare CMlLabel RaceTimer <=> (Root.GetFirstChild("race-timer") as CMlLabel);
	return K_{{{C}}}{
		Root = Root,
		Frame = Frame,
		RaceTimer = RaceTimer
	};
}

Void {{{C}}}_Update(K_{{{C}}} _RaceTimer){
	if(GUIPlayer == Null){
		_RaceTimer.Frame.Hide();
		return;
	}
	_RaceTimer.Frame.Show();
	_RaceTimer.RaceTimer.Value = ""^TL::TimeToText(GUIPlayer.CurrentRaceTime,True);
}
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Manialink::CreateComponent(C,[],_Model(),_Directives(),_Functions(),"");
}
