#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink
#Include "ChaseAlliance/Libs/Common/UI/Consts/Align.Script.txt" as Align
#Include "TextLib" as TL

#Const C "RaceTimer"

Text _Model (){
	return """
<framemodel id="{{{C}}}" class="{{{C}}}" {{{Align::CB}}}>
	<label id="race-timer" pos="0 -0.8" size="64 8" textfont="RajdhaniMono" textsize="8" {{{Align::CB}}}/>
</framemodel>
""";
}

Text _Directives(){
	return """//!
#Struct K_{{{C}}}{
	CMlFrame Root;
	CMlLabel RaceTimer;
}
""";
}

Text _Functions(){
	return """//!
K_{{{C}}} {{{C}}}_Get(CMlFrame _Parent,Text _Id) {
	declare CMlFrame Root <=> (_Parent.GetFirstChild(_Id) as CMlFrame);
	declare CMlLabel RaceTimer <=> (Root.GetFirstChild("race-timer") as CMlLabel);
	return K_{{{C}}}{
		Root = Root,
		RaceTimer = RaceTimer
	};
}

Void {{{C}}}_Update(K_{{{C}}} _RaceTimer,CSmPlayer _Player){
	if(_Player != Null){
		_RaceTimer.RaceTimer.Value = ""^TL::TimeToText(_Player.CurrentRaceTime,True);
		_RaceTimer.Root.Visible = True;
	}else{
		_RaceTimer.Root.Visible = False;
	}
}
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Manialink::CreateComponent(C,[],_Model(),_Directives(),_Functions(),"");
}
