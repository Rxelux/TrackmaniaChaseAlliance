#Include "ChaseAlliance/Libs/Common/UI/Manialink.Script.txt" as Manialink
#Include "ChaseAlliance/Libs/Common/UI/Consts/Align.Script.txt" as Align
#Include "TextLib" as TL

#Const C "LapCounter"

Text _Model (){
	return """
<framemodel id="{{{C}}}" class="{{{C}}}" {{{Align::CT}}}>
	<label id="prefix" pos="-24 -4.6" size="16 8" text="Lap" textfont="RajdhaniMono" textsize="6" textemboss="1" {{{Align::R2}}}/>
	<label id="lap" pos="0 -8" size="24 16" text="1/5" textfont="RajdhaniMono" textsize="12" textemboss="1" {{{Align::R2}}}/>
</framemodel>
""";
}

Text _Directives(){
	return """//!
#Struct K_{{{C}}}{
	CMlFrame Root;
	CMlLabel Lap;
	CMlLabel Prefix;
}
""";
}

Text _Functions(){
	return """//!
K_{{{C}}} {{{C}}}_Get(CMlFrame _Parent,Text _Id) {
	declare CMlFrame Root <=> (_Parent.GetFirstChild(_Id) as CMlFrame);
	declare CMlLabel Lap <=> (Root.GetFirstChild("lap") as CMlLabel);
	declare CMlLabel Prefix <=> (Root.GetFirstChild("prefix") as CMlLabel);
	return K_{{{C}}}{
		Root = Root,
		Lap = Lap,
		Prefix = Prefix
	};
}

Void {{{C}}}_Update(K_{{{C}}} _LapCounter,CSmPlayer _Player,Integer _Total){
	if(_Player != Null){
		_LapCounter.Lap.Value = ""^_Player.CurrentLapNumber+1^"/"^_Total;
		_LapCounter.Root.Visible = True;
	}else{
		_LapCounter.Root.Visible = False;
	}
}
""";
}

declare Boolean G_Loaded;
Void Unload() {
	G_Loaded = False;
}

Void Load() {
	if(G_Loaded)return;
	Unload();
	G_Loaded = True;
	Manialink::Load();
	Manialink::CreateComponent(C,[],_Model(),_Directives(),_Functions(),"");
}
